Alphabet

 a â b c ç d e f g ğ h ı i j k l m n ñ o ö p q r s ş t u ü v y z
 A Â B C Ç D E F G Ğ H I   J K L M N Ñ O Ö P Q R S Ş T U Ü V Y Z


 %{i%}:i ! %{i%}:ı %{i%}:u %{i%}:ü
 %{y%}:0 ! %{y%}:i %{y%}:ı %{y%}:u %{y%}:ü
 %{I%}:i ! %{I%}:ı
 %{Y%}:i ! %{Y%}:ı %{Y%}:u %{Y%}:ü
 %{A%}:a ! %{A%}:e
 %{C%}:c %{C%}:ç
 %{D%}:d ! %{D%}:t 
 %{G%}:g ! %{G%}:q %{G%}:ğ %{G%}:k
 %{E%}:a ! %{E%}:e %{E%}:y
 %{s%}:0 ! %{s%}:s
 %{n%}:0 ! %{n%}:n
 %{l%}:l ! %{l%}:n
 %{j%}:0 %{j%}:y
 %{U%}:u

 %{ö%}:0 %{o%}:0 %{e%}:0 %{ı%}:0 
 %{b%}:0 %{p%}:0 

 %{ь%}:0
 %{ъ%}:0
 %{☭%}:0

 %- 
 %%

 %>:0 ;

Sets

Vow = a â e ı i o u ö ü 
      A Â E I   O U Ö Ü ; 

Cns = b c ç d f g ğ h j k l m n ñ r p q r s ş t v y z 
      B C Ç D F G Ğ H J K L M N Ñ R P Q R S Ş T V Y Z ; 

NasalCns = m n ñ
           M N Ñ ; 

VoicelessCns = ç f h k p q s ş t
               Ç F H K P Q S Ş T ;

VoicedCns = c d g ğ j l m n ñ r v y z
            C D G Ğ J L M N Ñ R V Y Z ;

FrontVow = e i ö ü
           E İ Ö Ü ;

BackVow = a â ı o u
          A Â I O U ;

AbstractVow = %{ö%} %{o%} %{e%} %{ı%} ;


Rules

!"Voicing at morpheme boundary"
!Cx:Cy <=>  Vow _ %>: %{s%}:0 :Vow ; 
!               _ %>: %{I%}:  ;
!      except
!           _ %{☭%}: %>: ; 
!      where Cx in ( k p ) ! t 
!            Cy in ( g b ) ! d
!      matched ;

!"Devoicing of non-coronal stem-final voiceless consonants"
!Cx:Cy <=> _ .#. ;
!          _ %>: :Cns/:0* ;
!      except
!           _ %{☭%}: %>: ; 
!      where Cx in ( b g ğ )
!            Cy in ( p k q )
!      matched ;


"Voicing of non-coronal stem-final voiceless consonants"
!Cx:Cy <=> :Cns _ %>: [ \:Cns ]/:0* ;
! changed this because cf. mülküñüz
Cx:Cy <=> :Vow/:0* _ %>: :Vow/:0* ;
      except
          _ %{☭%}: %>: ; 
      where Cx in ( p k q )
            Cy in ( b g ğ )
      matched ;



! k e r e k:g >:0 {I}:i m
! k l a s s i k >:0 {s}:0 {I}:i {n}:0
! k i t a p:b >:0 {s}:0 {I}:ı {n}:0
! y a p >:0 {E}:a 
! t a p >:0 {i}:ı p

"Devoicing of coronal stem-final voiceless consonants"
Cx:Cy <=> :Vow _ .#. ;
!!          _ %>: [ :Cns - %{D%}: ]/:0* ;              !! FMT: See maqsatta
          _ %>: [ :Cns :? - [ %{D%}: %{I%}: ] ]/:0* ;
      except
          _ %{☭%}: %>: ; 
      where Cx in ( d c j )
            Cy in ( t ç ş )
      matched ;

! m a q s a d:t >:0 {D}:t {A}:a
! m a q s a d:d >:0 {s}:0 {I}:ı {n}:0

! q a y d

"Deletion of dative {G} after px1sg, px2sg"
%{G%}:0 <=> [ %{I%}: | %{i%}: ] :NasalCns %>: _ %{A%}: .#. ;

! ö z b a ş >:0 {i}:ı {n}:n >:0 {G}:0 {A}:a

!-!             %{G%}: %>:0 _ ;
!-! y a p >:0 m {A}:a {G}:ğ >:0 {G}:0 {A}:a

"Epenthesis in some stems"
%{y%}:0 <=> _ [ :Cns :Vow ]/[ :0 | %>: ] ;

"Voicing assimilation of {D}"
%{D%}:t <=> [ :VoicelessCns ]/[ :0 ]* _ ;


!!!!! VOWEL HARMONY !!!!!

"Vowel harmony for {A}"
%{A%}:e <=> [ :FrontVow :Cns :Cns* ]/[ :0 - %{ъ%}: ]* _ ;
            [ :BackVow :Cns :Cns* %{ь%}:0 :Cns* ]/[ :0 ]* _ ;
            [ %{e%}:0 | %{ö%}:0 ]/[ :0 | :Cns | %- | %% ]* _ ;

! 1939{o}{b}>{G}{A}

"Vowel harmony for {E}"
%{E%}:e <=> [ :FrontVow :Cns :Cns* ]/[ :0 - %{ъ%}: ]* _ ;
            [ :BackVow :Cns :Cns* %{ь%}:0 :Cns* ]/[ :0 ]* _ ;
            [ %{e%}:0 | %{ö%}:0 ]/[ :0 | :Cns | %- | %% ]* _ ;


"Vowel harmony for {U}"
%{U%}:ü <=> [ :FrontVow :Cns :Cns* ]/:0* _ ;

"No {U} or {i} after vowels"
Vx:0 <=> :Vow/:0* _ ;
     where Vx in ( %{U%} %{i%} ) ;

"Vowel harmony for {Y} in >2nd σ"
%{Y%}:Vy <=> :Vx :Cns+/:0* _ ;
         where Vx in ( i e ü ö a â ı o u )
               Vy in ( i i i i ı ı ı ı ı )
         matched ;

"Vowel harmony for {I} in >2nd σ"
%{I%}:Vy <=> :Vx :Cns+/:0* _ ;
         except
             .#. :Cns* :Vx :Cns+/:0* _ ;
         where Vx in ( i e ü ö a â ı o u )
               Vy in ( i i i i ı ı ı ı ı )
         matched ;

"Vowel harmony for {i} in >2nd σ"
%{i%}:Vy <=> :Vx :Cns+/:0* _ ;
         except
             .#. :Cns* :Vx :Cns+/:0* _ ;
         where Vx in ( i e ü ö a â ı o u )
               Vy in ( i i i i ı ı ı ı ı )
         matched ;

"Vowel harmony for {I} in ≤2nd σ"
%{I%}:Vy <=> .#. :Cns* :Vx :Cns+/:0* _ ;
         where Vx in ( i e ü ö a â ı o u )
               Vy in ( i i ü ü ı ı ı u u )
         matched ;

"Vowel harmony for {i} in ≤2nd σ"
%{i%}:Vy <=> .#. :Cns* :Vx :Cns+/:0* _ ;
         where Vx in ( i e ü ö a â ı o u )
               Vy in ( i i ü ü ı ı ı u u )
         matched ;

"Vowel harmony for {y} unless next C starts a syllable"
%{y%}:Vy <=> .#. :Cns* :Vx :Cns+/:0* _ [ :Cns [ :Cns | .#. ] ]/:0* ;
         where Vx in ( i e ü ö a â ı o u )
               Vy in ( i i ü ü ı ı ı u u )
         matched ;



!!!!! DORSAL CONSONANTS !!!!!

"Voiced back version of {G}"
%{G%}:ğ <=> :ğ/:0* _ ;
            [ :BackVow ( :Cns* :VoicedCns ) ]/[ :0 - %{ь%}: ]* _ :Vow/:0 ;
        except
            [ %{I%}: | %{i%}: ] :NasalCns %>: _ %{A%}: .#. ;

"Unvoiced back version of {G}"
%{G%}:q <=> :q/:0* _ ;
            [ :BackVow :Cns* [ :VoicelessCns - :k ] ]/[ :0 - %{ь%}: ]* _ ;
            [ :BackVow ]/[ :0 - %{ь%}: ]* _ .#. ;
            [ :BackVow ]/[ :0 - %{ь%}: ]* _ :Cns/:0 ;
        except
            [ %{I%}: | %{i%}: ] :NasalCns %>: _ %{A%}: .#. ;

"Unvoiced front version of {G}"
%{G%}:k <=> :k/:0* _ ;
            [ :FrontVow :Cns* [ :VoicelessCns - :q ] ]/[ :0 - %{ъ%}: ]* _ ;
            [ :FrontVow ]/[ :0 - %{ъ%}: ]* _ .#. ;
            [ :FrontVow ]/[ :0 - %{ъ%}: ]* _ :Cns/:0 ;
        except
            [ %{I%}: | %{i%}: ] :NasalCns %>: _ %{A%}: .#. ;



!!!!! MORPHOLOGICAL STUFF !!!!!

"{n} as n in certain possession + case combinations"
%{n%}:n <=> _ %>: [ %{G%}: | %{D%}: | %{C%}: ] ;

!@ f:f i:i k:k i:i r:r >:0 {s}:0 {I}:i {n}:n >:0 {C}:c {A}:e

"{s} after vowels"
%{s%}:s <=> :Vow :0* _ ;

"{l} as n after l"
%{l%}:n <=> :l :Vow/:0* _ ;

"{E} to y after vowel"
%{E%}:y <=> :Vow [ :0 - AbstractVow: ]* _ ;


